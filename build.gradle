buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven { url = "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.9"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: "com.matthewprenger.cursegradle"

ext.build = System.getenv('TRAVIS_BUILD_NUMBER')
ext.tag = System.getenv("TRAVIS_TAG")
version = "1.12.2-1.0"
version =  version + "-build-T" + ext.build
group= "com.austinv11.collectiveframework" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "TheFramework"

minecraft {
    version = "1.12.2-14.23.0.2486"
    mappings = "snapshot_20170626"
    runDir = "eclipse"
    replace("@VERSION@", project.version)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourceJar
            artifact deobfJar
        }
    }
}

// deobf jars
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest {
        attributes 'FMLAT': 'cf_at.cfg',
                'FMLCorePlugin': 'com.austinv11.collectiveframework.minecraft.asm.CollectiveFrameworkEarlyTransformerPlugin',
                'FMLCorePluginContainsFMLMod': true
    }
}

if (ext.tag) {
    if (ext.tag.contains("release"))
        ext.releaseType = "release"
    else if (ext.tag.contains("beta"))
        ext.releaseType = "beta"
    else
        ext.releaseType = "alpha"
    curseforge {
        apiKey = System.getenv("CURSE_API_KEY") ?: ""
        project {
            id = "279748"
            changelog = System.getenv("TRAVIS_COMMIT_MESSAGE") ?: ""
            releaseType = project.releaseType
            mainArtifact jar
            addArtifact deobfJar
        }
    }
    bintray {
        user = 'rolandoislas'
        key = System.getenv("BINTRAY_API_KEY") ?: ""
        dryRun = false
        publish = true
        publications = ['mavenJava']
        pkg {
            repo = "The-Framework"
            name = "TheFramework"
            desc = "A Minecraft Mod Framework"
            websiteUrl = "https://github.com/rolandoislas/TheFramework"
            issueTrackerUrl = "https://github.com/rolandoislas/TheFramework/issues"
            vcsUrl = "https://github.com/rolandoislas/TheFramework.git"
            licenses = ["GPL v2"]
            publicDownloadNumbers = true
            version {
                name = project.version + "-" + project.releaseType
            }
        }
    }
}

dependencies {
    
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'cf_at.cfg',
                'FMLCorePlugin': 'com.austinv11.collectiveframework.minecraft.asm.CollectiveFrameworkEarlyTransformerPlugin',
                'FMLCorePluginContainsFMLMod': true
    }
    dependsOn deobfJar
}

idea {
    module {
        inheritOutputDirs = true
    }
}